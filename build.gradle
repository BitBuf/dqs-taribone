buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven { url = 'https://plugins.gradle.org/m2' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

version = "3.2.1"
group = "dev.dewy.dqs" 
archivesBaseName = "taribone"

sourceCompatibility = targetCompatibility = '1.8' 
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    coreMod = "dev.dewy.taribone.TariboneLoadingPlugin"
    
    mappings = "stable_39"
}

jar.manifest.attributes(
        'FMLCorePluginContainsFMLMod': true,
        'FMLCorePlugin': 'dev.dewy.taribone.TariboneLoadingPlugin',
        'ForceLoadAsMod': true,
        'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker'
)

repositories {
    jcenter()
    maven { url = 'https://repo.spongepowered.org/maven' }

    maven {
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }

    maven {
        name = 'impactdevelopment-repo'
        url = 'https://impactdevelopment.github.io/maven/'
    }
}

dependencies {
    compile('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        // Mixin includes a lot of dependencies that are too up-to-date
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
        exclude module: 'log4j-core'
    }

    compile ('net.dv8tion:JDA:4.1.1_165') {
        exclude module: 'opus-java'
    }

    compile group: 'com.jagrosh', name: 'jda-utilities', version: '3.0.3'
    compile group: 'net.daporkchop.lib', name: 'binary', version: '0.5.5-SNAPSHOT'

    provided 'cabaletta:baritone-api:1.2'
}

mixin {
    add sourceSets.main, 'mixins.taribone.refmap.json'
}

shadowJar {
    exclude 'dummyThing'
    exclude 'LICENSE.txt'

    classifier = 'all'
}

build.dependsOn(shadowJar)

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath = sourceSets.main.compileClasspath
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
